## Impact

The `PrelaunchPoints` contract currently lacks explicit slippage protection in its implementation of the `PrelaunchPoints::_fillQuote` function, which handles token swaps via an external `exchangeProxy` (using the 0x protocol). This oversight can lead to potential front-running attacks where malicious actors may observe pending transactions and execute trades that capitalize on the observed trades before they are confirmed. This risk is further exacerbated because the contract relies on the integrity of externally provided `_swapCallData` without validating the minimum output amount for slippage protection. If `_swapCallData` lacks stringent conditions on the minimum output amount, the transaction is susceptible to slippage, potentially resulting in financial losses for the users of the contract.

## Proof of Concept

The [`_fillQuote` function](https://github.com/code-423n4/2024-05-loop/blob/40167e469edde09969643b6808c57e25d1b9c203/src/PrelaunchPoints.sol#L491-L505) handles the swap but does not check for the actual output versus the expected minimum output.

```javascript
    function _fillQuote(IERC20 _sellToken, uint256 _amount, bytes calldata _swapCallData) internal {
        uint256 boughtETHAmount = address(this).balance;
        require(_sellToken.approve(exchangeProxy, _amount));
@>      (bool success,) = payable(exchangeProxy).call{value: 0}(_swapCallData);
        if (!success) {
            revert SwapCallFailed();
        }
@>      boughtETHAmount = address(this).balance - boughtETHAmount;
        emit SwappedTokens(address(_sellToken), _amount, boughtETHAmount);
    }
```

The [`_validateData` function](https://github.com/code-423n4/2024-05-loop/blob/40167e469edde09969643b6808c57e25d1b9c203/src/PrelaunchPoints.sol#L405-L442) validates input data but lacks checks for minimum received amounts in swap transactions.

## Tools Used

Manual review

## Recommended Mitigation Steps

Consider adding a `_minimumOut` parameter to the `claim`, `claimAndStake`, and `_claim` functions which is compared to an added return value of `amountReceived` from the `_fillQuote` function.

```diff
-   function claim(address _token, uint8 _percentage, Exchange _exchange, bytes calldata _data)
+   function claim(address _token, uint8 _percentage, Exchange _exchange, bytes calldata _data, uint256 _minimumOut)
        external
        onlyAfterDate(startClaimDate)
    {
-        _claim(_token, msg.sender, _percentage, _exchange, _data);
+        _claim(_token, msg.sender, _percentage, _exchange, _data, _minimumOut);
    }

-   function claimAndStake(address _token, uint8 _percentage, Exchange _exchange, bytes calldata _data)
+   function claimAndStake(address _token, uint8 _percentage, Exchange _exchange, bytes calldata _data, uint256 _minimumOut)
        external
        onlyAfterDate(startClaimDate)
    {
-        uint256 claimedAmount = _claim(_token, address(this), _percentage, _exchange, _data);
+        uint256 claimedAmount = _claim(_token, address(this), _percentage, _exchange, _data, _minimumOut);
        lpETH.approve(address(lpETHVault), claimedAmount);
        lpETHVault.stake(claimedAmount, msg.sender);

        emit StakedVault(msg.sender, claimedAmount);
    }

-   function _claim(address _token, address _receiver, uint8 _percentage, Exchange _exchange, bytes calldata _data)
+   function _claim(address _token, address _receiver, uint8 _percentage, Exchange _exchange, bytes calldata _data, uint256 _minimumOut)
        internal
        returns (uint256 claimedAmount)
    {
        uint256 userStake = balances[msg.sender][_token];
        if (userStake == 0) {
            revert NothingToClaim();
        }
        if (_token == ETH) {
            claimedAmount = userStake.mulDiv(totalLpETH, totalSupply);
            balances[msg.sender][_token] = 0;
            lpETH.safeTransfer(_receiver, claimedAmount);
        } else {
            uint256 userClaim = userStake * _percentage / 100;
            _validateData(_token, userClaim, _exchange, _data);
            balances[msg.sender][_token] = userStake - userClaim;

            // At this point there should not be any ETH in the contract
            // Swap token to ETH
-           _fillQuote(IERC20(_token), userClaim, _data);
+           uint256 receivedAmount = _fillQuote(IERC20(_token), userClaim, _data);
+           if (receivedAmount < _minimumOut) revert InsufficientOutputAmount(receivedAmount, _minimumOut);

            // Convert swapped ETH to lpETH (1 to 1 conversion)
            claimedAmount = address(this).balance;
            lpETH.deposit{value: claimedAmount}(_receiver);
        }
        emit Claimed(msg.sender, _token, claimedAmount);
    }
.
.
.
-   function _fillQuote(IERC20 _sellToken, uint256 _amount, bytes calldata _swapCallData) internal {
+   function _fillQuote(IERC20 _sellToken, uint256 _amount, bytes calldata _swapCallData) internal returns (uint256 amountReceived) {
        // Track our balance of the buyToken to determine how much we've bought.
-       uint256 boughtETHAmount = address(this).balance;
+       uint256 initialBalance = address(this).balance;

        require(_sellToken.approve(exchangeProxy, _amount));

        (bool success,) = payable(exchangeProxy).call{value: 0}(_swapCallData);
        if (!success) {
            revert SwapCallFailed();
        }

        // Use our current buyToken balance to determine how much we've bought.
-       boughtETHAmount = address(this).balance - boughtETHAmount;
-       emit SwappedTokens(address(_sellToken), _amount, boughtETHAmount);
+       amountReceived = address(this).balance - initialBalance;
+       emit SwappedTokens(address(_sellToken), _amount, amountReceived);
+       return amountReceived;
    }
```
